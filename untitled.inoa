#include <Arduino.h>
#include <stdlib.h>
#include <string.h>
#include <EnableInterrupt.h>
#include <NeoSWSerial.h>

// channel 2 is forward/reverse
// channel 1 is left/right

// serial initialisation
#define RX_PIN 10
#define TX_PIN 11
NeoSWSerial nSerial(RX_PIN, TX_PIN); // RX, TX

// battery variables
int MIN_BAT_LEVEL = 660; // just above 3.2V
int batteryPin = A1;
int LED = 13;
int batteryLevel = 0;

// communication variables
char message[50];

// rc variables
#define RC_CH1  0
#define RC_CH2  1

#define RC_NUM_CHANNELS 2

#define RC_CH1_INPUT  2
#define RC_CH2_INPUT  3

uint16_t rc_values[RC_NUM_CHANNELS];
uint32_t rc_start[RC_NUM_CHANNELS];
volatile uint16_t rc_shared[RC_NUM_CHANNELS];

void serialISR()
{
    NeoSWSerial::rxISR(*portInputRegister(digitalPinToPort(RX_PIN)));
}

void rc_read_values() {
    noInterrupts();
    memcpy(rc_values, (const void *)rc_shared, sizeof(rc_shared));
    interrupts();
}

void calc_input(uint8_t channel, uint8_t input_pin) {
    if (digitalRead(input_pin) == HIGH) {
        rc_start[channel] = micros();
    } else {
        uint16_t rc_compare = (uint16_t)(micros() - rc_start[channel]);
        rc_shared[channel] = rc_compare;
    }
}

void calc_ch1() { calc_input(RC_CH1, RC_CH1_INPUT); }
void calc_ch2() { calc_input(RC_CH2, RC_CH2_INPUT); }

void append(char* s, char c) {
    int len = strlen(s);
    if (len <= 49) {
        s[len] = c;
        s[len + 1] = '\0';
    }
}

void setup() {
    enableInterrupt(RX_PIN, serialISR, CHANGE);

    pinMode(batteryPin, INPUT);
    pinMode(LED, OUTPUT);

    digitalWrite(LED, LOW);

    Serial.begin(9600);
    Serial.println("connected");
    nSerial.begin(9600);
    nSerial.print("connected\n");

    pinMode(RC_CH1_INPUT, INPUT);
    pinMode(RC_CH2_INPUT, INPUT);

    enableInterrupt(RC_CH1_INPUT, calc_ch1, CHANGE);
    enableInterrupt(RC_CH2_INPUT, calc_ch2, CHANGE);
}

void loop() {
    // put your main code here, to run repeatedly:
    batteryLevel = analogRead(batteryPin);
    batteryLevel = MIN_BAT_LEVEL + 1;
    if (batteryLevel <= MIN_BAT_LEVEL) {
        digitalWrite(LED, HIGH);
        nSerial.print("battery low\n");
        Serial.println("battery low");
        delay(5000);
    }
    else {
        digitalWrite(LED, LOW);
        if (nSerial.available()) {
            char c = nSerial.read();

            if (c != '\n') {
                append(message, c);
                Serial.println(c);
            }
            else {
                Serial.println(message);
                if (strcmp(message, "readings?") == 0) {
                    rc_read_values();
                    Serial.print("Direction:");
                    Serial.print(rc_values[RC_CH1]);
                    Serial.print("\t");
                    Serial.print("Speed:");
                    Serial.println(rc_values[RC_CH2]);
                    nSerial.print(rc_values[RC_CH1]);
                    nSerial.print('\n');
                    nSerial.print(rc_values[RC_CH2]);
                    nSerial.print('\n');
                } else {
                    Serial.println("Received unknown command");
                }
                memcpy(message, "", sizeof(""));
            }
        }
    }
}